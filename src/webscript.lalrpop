use std::str::FromStr;
use webscript_lang::ast::*;

grammar;

Located<T>: Located<T> = <start:@L> <v:T> <end:@R> => Located::new(v, start..end);
Ident: String = <start:@L> <s:r"[a-zA-Z_]([a-zA-Z_0-9])*"> => s.to_string();
Int: i64 = <s:r"[0-9]+"> => i64::from_str(s).unwrap();
Float: f64 = <s:r"[0-9]+\.[0-9]+|[0-9]+\.|\.[0-9]+"> => f64::from_str(s).unwrap();
Char: char = <s:r"\'\\.\'|\'.\'"> => {
    let mut s = s.to_string();
    s.remove(0);
    s.pop();
    if s.starts_with('\\') {
        s.remove(0);
        match s.chars().next().unwrap() {
            'n' => '\n',
            't' => '\t',
            'r' => '\r',
            c => c
        }
    } else {
        s.remove(0)
    }
};
String: String = <s:r#""(\\.|[^"])*""#> => {
    let mut s = s.to_string();
    s.remove(0);
    s.pop();
    s.replace("\\n", "\n").replace("\\t", "\t").replace("\\r", "\r").replace("\\\"", "\"")
};
IdentString: String = {
    Ident, 
    String, 
};

Seperated<T, U>: Vec<T> = {
    <mut v:(<T> U)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};
Comma<T>: Vec<T> = Seperated<T, ",">;
CommaDelim<L, T, R>: Vec<T> = L <v:Comma<T>> R => v;

// AST

pub Module: Module = {
    <defs:Located<Definition>*> => Module(defs),
};
pub Definition: Definition = {
    "pub" "fn" <name:Located<IdentString>> <params:CommaDelim<"(", Located<Parameter>, ")">> <result:ResultType?> <body:Located<Body>> => Definition::Fn(Function {
        export: true,
        name,
        params,
        result: result.unwrap_or_default(),
        body,
    }),
    "fn" <name:Located<IdentString>> <params:CommaDelim<"(", Located<Parameter>, ")">> <result:ResultType?> <body:Located<Body>> => Definition::Fn(Function {
        export: false,
        name,
        params,
        result: result.unwrap_or_default(),
        body,
    }),
};
pub ResultType: Vec<Located<Type>> = ":" <types:Comma<Located<Type>>> => types;
pub Parameter: Parameter = {
    <name:Located<Ident>> ":" <typ:Located<Type>> => Parameter {
        name,
        typ,
    },
};
pub Body: Body = "{" <stats:Located<Statement>*> "}"=> Body(stats);
pub Block: Statement = "{" <stats:Located<Statement>*> "}" => Statement::Block(stats);
pub Statement: Statement = {
    "do" <block:Block> => block,
    // let
    "let" <param: Located<Parameter>> "=" <expr:Located<Expression>> => Statement::Let {
        param,
        expr
    },
    // if some
    "if" <param:Located<Parameter>> "=" <expr:Located<Expression>> <case:Located<Block>> "else" <else_case:Located<Statement>> => Statement::IfSome {
        param,
        expr,
        case: Box::new(case),
        else_case: Some(Box::new(else_case)),
    },
    "if" <param:Located<Parameter>> "=" <expr:Located<Expression>> <case:Located<Block>> "else" <else_case:Located<Block>> => Statement::IfSome {
        param,
        expr,
        case: Box::new(case),
        else_case: Some(Box::new(else_case)),
    },
    "if" <param:Located<Parameter>> "=" <expr:Located<Expression>> <case:Located<Block>> => Statement::IfSome {
        param,
        expr,
        case: Box::new(case),
        else_case: None,
    },
    // if
    "if" <cond:Located<Expression>> <case:Located<Block>> "else" <else_case:Located<Statement>> => Statement::If {
        cond,
        case: Box::new(case),
        else_case: Some(Box::new(else_case)),
    },
    "if" <cond:Located<Expression>> <case:Located<Block>> "else" <else_case:Located<Block>> => Statement::If {
        cond,
        case: Box::new(case),
        else_case: Some(Box::new(else_case)),
    },
    "if" <cond:Located<Expression>> <case:Located<Block>> => Statement::If {
        cond,
        case: Box::new(case),
        else_case: None,
    },
    // while some
    "while" <param:Located<Parameter>> "=" <expr:Located<Expression>> <body:Located<Block>> => Statement::WhileSome {
        param,
        expr,
        body: Box::new(body),
    },
    // while
    "while" <cond:Located<Expression>> <body:Located<Block>> => Statement::While {
        cond,
        body: Box::new(body),
    },
    // for
    "for" <param:Located<Parameter>> "=" <iter:Located<Expression>> <body:Located<Block>> => Statement::For {
        param,
        iter,
        body: Box::new(body),
    },
    // control flow
    "break" => Statement::Break,
    "continue" => Statement::Continue,
    "return" <expr:Located<Expression>> => Statement::Return(expr),
    // assign
    <path:Located<Path>> <op:AssignOperator> <expr:Located<Expression>> => Statement::Assign {
        op,
        path,
        expr
    },
    // call
    <path:Located<Path>> <args:CommaDelim<"(", Located<Expression>, ")">> => Statement::Call {
        path,
        args,
    },
};
pub AssignOperator: AssignOperator = {
    "=" => AssignOperator::Equal,
    "+=" => AssignOperator::Plus,
    "-=" => AssignOperator::Minus,
    "*=" => AssignOperator::Star,
    "/=" => AssignOperator::Slash,
    "%=" => AssignOperator::Percent,
    "^=" => AssignOperator::Exponent,
};

pub Expression: Expression = {
    <expr:Chain> => expr,
};

Binary<Self, Op, Next>: Expression = {
    <left:Located<Self>> <op:Op> <right:Located<Next>> => Expression::Binary {
        op: op,
        left: Box::new(left),
        right: Box::new(right),
    },
    Next,
};
Unary<Self, Op, Next>: Expression = {
    <op:Op> <right:Located<Self>> => Expression::Unary {
        op: op,
        right: Box::new(right),
    },
    Next,
};
pub Chain = Binary<Chain, ChainOperator, Logic>;
ChainOperator: BinaryOperator = {
    "->" => BinaryOperator::Then,
};
pub Logic = Binary<Logic, LogicOperator, Not>;
LogicOperator: BinaryOperator = {
    "and" => BinaryOperator::And,
    "or" => BinaryOperator::Or,
};
pub Not = Unary<Not, NotOperator, Comparison>;
NotOperator: UnaryOperator = {
    "not" => UnaryOperator::Not,
};
pub Comparison = Binary<Comparison, ComparisonOperator, Arithmetic>;
ComparisonOperator: BinaryOperator = {
    "==" => BinaryOperator::EqualEqual,
    "!=" => BinaryOperator::ExclamationEqual,
    "<" => BinaryOperator::Less,
    ">" => BinaryOperator::Greater,
    "<=" => BinaryOperator::LessEqual,
    ">=" => BinaryOperator::GreaterEqual,
};
pub Arithmetic = Binary<Arithmetic, ArithmeticOperator, Term>;
ArithmeticOperator: BinaryOperator = {
    "+" => BinaryOperator::Plus,
    "-" => BinaryOperator::Minus,
};
pub Term = Binary<Term, TermOperator, Exponent>;
TermOperator: BinaryOperator = {
    "*" => BinaryOperator::Star,
    "/" => BinaryOperator::Slash,
    "%" => BinaryOperator::Percent,
};
pub Exponent = Binary<Exponent, ExponentOperator, Negative>;
ExponentOperator: BinaryOperator = {
    "^" => BinaryOperator::Exponent,
};
pub Negative = Unary<Negative, NegativeOperator, Call>;
NegativeOperator: UnaryOperator = {
    "-" => UnaryOperator::Minus,
};
pub Call: Expression = {
    <head:Located<Call>> <args:CommaDelim<"(", Located<Expression>, ")">> => Expression::Call {
        head: Box::new(head),
        args,
    },
    Field,
};
pub Field: Expression = {
    <head:Located<Field>> "." <field:Located<Ident>> => Expression::Field {
        head: Box::new(head),
        field: field,
    },
    <head:Located<Field>> "." <field:Located<String>> => Expression::Field {
        head: Box::new(head),
        field: field,
    },
    <head:Located<Field>> "[" <index:Located<Expression>> "]" => Expression::Index {
        head: Box::new(head),
        index: Box::new(index),
    },
    <atom:Atom> => Expression::Atom(atom),
};

pub Atom: Atom = {
    <ident:Ident> => match ident.as_str() {
        "null" => Atom::Null,
        "true" => Atom::Bool(true),
        "false" => Atom::Bool(false),
        _ => Atom::Ident(ident),
    },
    <v:Int> => Atom::Int(v),
    <v:Float> => Atom::Float(v),
    <v:Char> => Atom::Char(v),
    <v:String> => Atom::String(v),
    <mut v:CommaDelim<"(", Located<Expression>, ")">> => if v.len() == 1 {
        let expr = v.remove(0);
        Atom::Expression(Box::new(expr))
    } else {
        Atom::Tuple(v)
    },
    <v:CommaDelim<"[", Located<Expression>, "]">> => Atom::Vector(v),
    <v:CommaDelim<"@{", KeyValue, "}">> => Atom::Object(v),
};
pub KeyValue: (Located<String>, Located<Expression>) = {
    <key:Located<Ident>> "=" <value:Located<Expression>> => (key, value),
    <key:Located<String>> "=" <value:Located<Expression>> => (key, value),
};

pub Path: Path = {
    <head:Located<Path>> "." <field:Located<Ident>> => Path::Field {
        head: Box::new(head),
        field,
    },
    <head:Located<Path>> "." <field:Located<String>> => Path::Field {
        head: Box::new(head),
        field,
    },
    <head:Located<Path>> "[" <index:Located<Expression>> "]" => Path::Index {
        head: Box::new(head),
        index: Box::new(index),
    },
    <ident:Ident> => Path::Ident(ident),
};

pub Type: Type = {
    "bool" => Type::Bool,
    "i32" => Type::Num(NumType::I32),
    "i64" => Type::Num(NumType::I64),
    "u32" => Type::Num(NumType::U32),
    "u64" => Type::Num(NumType::U64),
    "f32" => Type::Num(NumType::F32),
    "f64" => Type::Num(NumType::F64),
    "v128" => Type::Vec,
};